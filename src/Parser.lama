-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}


fun bincheck (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                              assertValue (a, Binop (op, 
                              Binop ("!=", l (Val), Const(0)), 
                              Binop ("!=", r (Val), Const(0))), loc)                              
                          }
                        }
  ]
}


  var insideIf = memo $ eta syntax (
      e=exp   kThen   e1=exp               {fun(a) {assertVoid(a, If(e (Val), e1 (Void), Skip), pos)}} |
      e=exp   kThen   e1=exp  kElse e2=exp       {fun(a) {If(e (Val), e1 (a), e2 (a))}} |
      e=exp   kThen   e1=exp  kElif e2=insideIf  {fun(a) {If(e (Val), e1 (a), e2 (a))}}   


  ),

  primary  = memo $ eta syntax (

                                  -- decimal constant
                                  loc=pos x=decimal                                 {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  x=lident                                          {fun (a) {
                                                                                       case a of
                                                                                         Ref  -> Ref (x)
                                                                                       | Void -> Ignore (Var (x))
                                                                                       | _    -> Var (x)
                                                                                       esac
                                                                                    }} |
                                  loc=pos kWhile e=exp x=inbr[kDo, exp, kOd] {fun (a) { assertVoid (a, While (e (Val), x (Void)), loc)} } |

                                  loc=pos kDo x=exp kWhile e=exp kOd {fun (a) { assertVoid (a, DoWhile (x (Void), e (Val)), loc)} } |

                                  loc=pos kFor e1=exp s[","] e2=exp s[","] e3=exp kDo x=exp kOd {fun (a) {assertVoid (a, Seq(e1 (Void), While (e2(Val), Seq(x (Void), e3 (Void)))), loc)}} |

                                  loc=pos kIf x=insideIf kFi {fun (a) {x(a)}} |

                                  loc=pos kWrite x=inbr[s("("), exp, s(")")] {fun (a) {assertVoid(a, Write(x (Val)), loc)}} |

                                  loc=pos kRead x=inbr[s("("), exp, s(")")] {fun (a) {assertVoid(a, Read(x (Ref)), loc)}}   |
        
                                  loc=pos kSkip {fun (a) {assertVoid(a, Skip, loc)}} |
                                  
                                  x=inbr[s("("), exp, s(")")]                 {fun (a) {x (a)}}),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (bincheck, {"!!"})],
                                  [Left , map (bincheck, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary)),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
